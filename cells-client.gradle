plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {

    namespace 'com.pydio.android.cells'

    compileSdkVersion ownVersions['android.sdk.compile'] as int
    buildToolsVersion ownVersions['android.build.tools']

    // Gather overrides in a distinct folder for easier maintenance
    sourceSets.main.res.srcDir 'src/main/res-overrides'

    defaultConfig {

        // Main legacy App ID
        applicationId "com.pydio.android.Client"
        // Test ID to be able to have the next app along with the production one on a single device
        // applicationId "com.pydio.android.cells.next"

        versionCode ownVersions['cells-app.code'] as int
        versionName ownVersions['cells-app.name']

        minSdkVersion ownVersions['android.sdk.min'] as int
        targetSdkVersion ownVersions['android.sdk.target'] as int

        consumerProguardFiles 'consumer-rules.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file(configs['keystore.path'])
            storePassword configs['keystore.pwd']
            keyAlias configs['signkey.alias']
            keyPassword configs['signkey.pwd']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"

            minifyEnabled true
            shrinkResources true

//            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            // versionName ownVersions['sdk-android.version.dev']
            // versionName = ownVersions['sdk-android.future.name']
            versionNameSuffix = '-dev'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        // dataBinding = true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.4"
    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    // We do not want that the linter makes builds fail. It is quite extremist by default.
    lintOptions {
        abortOnError false
    }
}

dependencies {

    if (useLocalSdkJava) {
        api project(":sdk-java")
        println "[DEBUG] Using local java SDK"
    } else {
        println "[WARNING] **Not** using local SDK "
        api "com.pydio.cells:cells-sdk-java:${ownVersions['java.sdk']}"
        println "[DEBUG] Retrieve java sdk from maven repo"
    }

    // dependencies are defined in root build.gradle file
    implementation libraries
    kapt librariesKapt

    implementation platform('androidx.compose:compose-bom:2023.03.00')
    implementation composeLibraries

    implementation "androidx.palette:palette-ktx:1.0.0"
    // override default platform version 
    implementation "androidx.compose.material3:material3:1.1.0-beta01"

    // Tmp addition
    implementation "androidx.datastore:datastore-preferences:1.0.0"


    // Enable custom data loader for glide library (display images)
    annotationProcessor processors

    testImplementation testLibraries
    androidTestImplementation androidTestLibraries
}
