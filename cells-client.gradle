plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {

    namespace 'com.pydio.android.cells'
    compileSdk ownVersions['android.sdk.compile'] as int

    // Gather overrides in a distinct folder for easier maintenance
    sourceSets.main.res.srcDir 'src/main/res-overrides'

    defaultConfig {
        // Main legacy App ID
        applicationId "com.pydio.android.Client"
        // Test ID to be able to have the next app along with the production one on a single device
        // applicationId "com.pydio.android.cells.next"

        versionCode ownVersions['cells-app.code'] as int
        versionName ownVersions['cells-app.name']

        minSdkVersion ownVersions['android.sdk.min'] as int
        targetSdkVersion ownVersions['android.sdk.target'] as int

        consumerProguardFiles 'consumer-rules.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file(configs['keystore.path'])
            storePassword configs['keystore.pwd']
            keyAlias configs['signkey.alias']
            keyPassword configs['signkey.pwd']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            resValue "string", "app_version",
                    "${defaultConfig.versionName}"

            minifyEnabled true
            // If you haven't already built your app using minifyEnabled for code shrinking,
            // then try that before enabling shrinkResources, because you might need to edit your proguard-rules.pro file
            // to keep classes or methods that are created or invoked dynamically before you start removing resources.
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // See https://developer.android.com/build/shrink-code#android_gradle_plugin_version_41_or_later
            // And you must have this installed in Android Studio https://developer.android.com/studio/projects/install-ndk
            // to also generate and upload the file that enables understanding stack traces seen in production
            // The generated mapping.txt file can than be uploaded on the store following this:
            // https://support.google.com/googleplay/android-developer/answer/9848633#upload_file&zippy=%2Cupload-files-using-play-console
            ndk {
                debugSymbolLevel 'FULL'
            }
        }

        debug {
            versionNameSuffix = '-dev'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        // See  https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        // To get the list of matching compiler depending on the Kotlin version
        kotlinCompilerExtensionVersion = "1.5.13"
    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    // We do not want that the linter makes builds fail. It is quite extremist by default.
    lintOptions {
        abortOnError false
    }
}

dependencies {
    if (useLocalSdkJava) {
        api project(":sdk-java")
        println "[DEBUG] Using local java SDK"
    } else {
        println "[WARNING] **Not** using local SDK "
        api "com.pydio.cells:cells-sdk-java:${ownVersions['java.sdk']}"
        println "[DEBUG] Retrieve java sdk from maven repo"
    }

    // Dependencies and versions are defined in the toml catalog in <root>/gradle folder
    implementation libs.androidx.junit.ktx

    // Latest version can be found here: https://developer.android.com/jetpack/compose/bom
    implementation platform(libs.compose.bom)

    // Dependency injection with Koin https://insert-koin.io/
    implementation platform(libs.koin.bom)
    implementation libs.koin.core
    implementation libs.koin.android
    implementation libs.koin.androidx.workmanager
    implementation libs.koin.androidx.navigation
    implementation libs.koin.androidx.compose

    // Tmp addition
    implementation libs.palette.ktx
    // To enable legacy preferences migration
    implementation libs.datastore.preferences

    // Enable custom data loader for glide library (display images)

    // Compose libraries
    implementation libs.androidx.runtime
    implementation libs.androidx.ui
    implementation libs.androidx.ui.util
    implementation libs.androidx.ui.tooling
    implementation libs.androidx.foundation
    implementation libs.androidx.foundation.layout
    implementation libs.androidx.material3
    implementation libs.androidx.material3.windowsizeclass
    // We use material 3, so remove remaining code that depend on this and remove the dep.
    implementation libs.androidx.material
    // This library is also provided as an API dependency by androidx.compose.material:material.
    // We specify it explicitly so that we won't break when we finally remove material lib from
    // the explicit dependency tree
    implementation libs.androidx.material.icons.core
    // this separate library, the full set of Material icons. Due to the very large size of this lib,
    // Make sure to use R8/Proguard to strip unused icons when including this as a direct dependency.
    // See https://developer.android.com/reference/kotlin/androidx/compose/material/icons/package-summary
    implementation libs.androidx.material.icons.extended

    // Include extensions the permits integration of Compose with extra TP libraries
    implementation libs.androidx.navigation.compose
    implementation libs.insert.koin.koin.androidx.compose

    // A few specific libraries
    implementation libs.zxing
    implementation libs.kotlin.stdlib
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    // Android UI and appcompat
    implementation libs.androidx.core
    implementation libs.androidx.appcompat
    // Navigation UI
    implementation libs.androidx.navigation.ui.ktx
    // ViewModel
    implementation libs.androidx.lifecycle.viewmodel.ktx
    // Extended UI features: prefs and splash screen
    implementation libs.androidx.preference
    implementation libs.androidx.core.splashscreen
    // Material UI => we still need this to build custom palette based on remote server custom primary color
    implementation libs.material
    // Room for database, work for background
    implementation libs.androidx.room.ktx
    implementation libs.androidx.work.runtime.ktx
    implementation libs.aws.android.sdk.s3
    implementation libs.glide
    implementation libs.commons.codec
    implementation libs.gson

    // Tests
    testImplementation libs.junit
    testImplementation libs.koin.test
    testImplementation libs.koin.test.junit4
    testImplementation libs.kotlinx.coroutines.test

    // Unfortunately, we cannot inherit from "simple" test dependencies.
    androidTestImplementation libs.koin.test
    androidTestImplementation libs.koin.test.junit4
    androidTestImplementation libs.kotlinx.coroutines.test
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.espresso.core


    kapt libs.androidx.room.compiler
    kapt libs.androidx.room.runtime
    kapt "com.github.bumptech.glide:compiler:5.0.0-rc01"

    implementation libs.glide.compose
    annotationProcessor libs.glide.compiler

}
